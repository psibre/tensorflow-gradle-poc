plugins {
    id "com.jetbrains.python.envs" version "0.0.25"
    id 'application'
}

mainClassName = 'PredictExampleTF'

ext {
    pyBootstrapDir = file("$buildDir/bootstrap")
    pyEnvName = 'Miniconda3-4.5.4'
}

extensions.create 'tensorflow', TensorFlowExtension, project

tensorflow {
    version = '1.5.0'
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.tensorflow', name: 'tensorflow', version: tensorflow.version
}

if (hasProperty('gpu')) {
    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('org.tensorflow:libtensorflow_jni') with module("org.tensorflow:libtensorflow_jni_gpu:$tensorflow.version")
            }
        }
    }
}

envs {
    bootstrapDirectory = pyBootstrapDir
    conda pyEnvName, pyEnvName, [condaPackage(tensorflow.package)]
}

task trainModel(type: TrainModel) {
    srcDir = file('python_part')
    destDir = layout.buildDirectory.dir('data')
    afterEvaluate {
        dependsOn build_envs
    }
}

run {
    dependsOn trainModel
    workingDir buildDir
}

class TensorFlowExtension {

    Project project
    Property<String> tfVersion = project.objects.property(String)

    TensorFlowExtension(Project project) {
        this.project = project
    }

    void setVersion(String version) {
        tfVersion.set(version)
    }

    String getVersion() {
        tfVersion.get()
    }

    String getPackage() {
        def packageName = project.hasProperty('gpu') ? 'tensorflow-gpu' : 'tensorflow'
        "$packageName==$version"
    }
}

class TrainModel extends DefaultTask {

    @InputDirectory
    final DirectoryProperty srcDir = newInputDirectory()

    @OutputDirectory
    final DirectoryProperty destDir = newOutputDirectory()

    @TaskAction
    void train() {
        project.copy {
            from srcDir
            into temporaryDir
        }
        project.exec {
            commandLine project.file("$project.pyBootstrapDir/$project.pyEnvName/bin/python"), 'train.py'
            workingDir temporaryDir
        }
        project.copy {
            from "$temporaryDir/data"
            from "$project.rootDir/data"
            into destDir
        }
    }
}
