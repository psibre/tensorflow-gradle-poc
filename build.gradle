plugins {
    id 'application'
}

mainClassName = 'PredictExampleTF'

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.tensorflow', name: 'tensorflow', version: '1.5.0-rc1'
}

if (hasProperty('gpu')) {
    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('org.tensorflow:libtensorflow_jni') with module('org.tensorflow:libtensorflow_jni_gpu:1.5.0-rc1')
            }
        }
    }
}

task trainModel(type: TrainModel) {
    srcDir = file('python_part')
    destDir = layout.buildDirectory.dir('data')
}

class TrainModel extends DefaultTask {

    @InputDirectory
    final DirectoryProperty srcDir = newInputDirectory()

    @OutputDirectory
    final DirectoryProperty destDir = newOutputDirectory()

    @TaskAction
    void train() {
        project.copy {
            from srcDir
            into temporaryDir
        }
        project.exec {
            commandLine 'pipenv', '--three', 'install'
            workingDir temporaryDir
        }
        project.exec {
            commandLine 'pipenv', 'run', 'python3', 'train.py'
            workingDir temporaryDir
        }
        project.copy {
            from "$temporaryDir/data"
            into destDir
        }
    }
}
